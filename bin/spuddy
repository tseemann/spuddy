#!/usr/bin/env perl
use 5.32.0;
use strict;
use warnings;
use List::Util qw(min max sum);
use Getopt::Std;
use File::Basename;
use IO::Uncompress::AnyUncompress qw(anyuncompress $AnyUncompressError);
use Data::Dumper;
use JSON;
use Path::Tiny;

#......................................................................................
my $VERSION = "0.0.3";
my $EXE = basename($0);
my $URL = 'https://github.com/tseemann/spuddy';

my %opt = (
  'D' => 'kmers.7.1.50.db.90pc',
  'T' => 11,
  'L' => 300,
  'K' => '/dev/null',
);
#......................................................................................
sub usage {
  my($errcode) = @_;
  $errcode ||= 0;
  my $ofh = $errcode ? \*STDERR : \*STDOUT;
  print $ofh 
    "NAME\n  $EXE $VERSION - bacterial species prediction\n",
    "USAGE\n  $EXE [opts] file.{fasta,fastq}[.gz]\n",
    "OPTIONS\n",
    "  -h       Print this help\n",
    "  -v       Print version and exit\n",
    "  -C       Show citation and exit\n",
    "  -q       No output while running, only errors\n",
    "SETTINGS\n",
    "  -D FILE  Database file [$opt{D}]\n",
    "  -T CODE  Genetic code [$opt{T}]\n",
    "  -L LEN   Minimum ORF length [$opt{L}]\n",
    "  -K FILE  Save kmers to this file [$opt{K}]\n",
    "HOMEPAGE\n  $URL\n",
    "END\n";
  exit($errcode);
}
#......................................................................................
sub msg { print STDERR "@_\n" unless $opt{'q'}; }
sub wrn { msg("WARNING:", @_); }
sub err { print STDERR "ERROR: @_\n"; exit(-1); }
#......................................................................................
getopts('vhqCT:D:L:', \%opt) or exit(-1);
$opt{'v'} and version();
$opt{'C'} and citation();
$opt{'h'} and usage(0);
@ARGV or usage(1);

is_int($opt{'T'}, "-T", 1, 36);
is_int($opt{'L'}, "-L", 180, 999);
is_file($opt{'D'}, "-D");

#......................................................................................
msg("This is $EXE $VERSION");

my $outfile = Path::Tiny->tempfile();

my $cmd = qq/
   orfm -c $opt{T} -s -m $opt{L} @ARGV
 |  seqkit sliding -w 0 -W 7 -s 1
 | grep -Ev '^>|[XUOW*]'
 | LC_ALL=C sort
 | tee out.kmers
 | LC_ALL=C join -t \$'\\t' -j 1 - $opt{D}
 > $outfile
/;
$cmd =~ s/\s+/ /xmsg;
msg("Running: $cmd");
msg("Scannubg: $opt{D}");
system($cmd)==0 or die $!;
my %score;
my %match;
foreach ($outfile->lines({chomp=>1})) {
  # LYKYKVA 0.945946 105 Bacteria/Coprococcus/acet
  my(undef,$p,$n,$gs) = split m/\t/;
  $score{$gs} += $p;
  $match{$gs} ++;
}
my $sum_score = sum(values %score);
my $sum_match = sum(values %match);
for my $gs (sort { $score{$b} <=> $score{$a} } keys %score) {
  next if $match{$gs} <= 1;
  printf "%6.2f%% %6.2f%% %5d %s\n",
    $score{$gs}*100/$sum_score,
    $match{$gs}*100/$sum_match,
    $match{$gs},
    $gs;
}

msg("Done.");
exit(0);

#......................................................................................
sub is_file {
  my($fname, $name) = @_;
  -r $fname or err("-D $fname is not a valid file") ;
} 
#......................................................................................
sub is_int {
  my($s, $name, $min, $max) = @_;
  $s =~ m/^[-+]?\d+$/ && $s >= $min && $s <= $max
    or err("$name must be an integer between $min and $max");
} 
#......................................................................................
sub version { 
  say "$EXE $VERSION"; 
  exit(0); 
}
#......................................................................................
sub citation {
  say "Seemann T (2026), '$EXE' Github $URL";
  exit(0);
}
#......................................................................................
sub load_fasta {
  my($fh) = @_;
  my @seq;
  while (<$fh>) {
    chomp;
    if (m/^\s*>(\S+)/) {
      push @seq, [ $1, '' ];
    }
    else {
      @seq or err("Error parsing FASTA");
      $seq[-1]->[1] .= uc($_);
    }
  }
  msg("Loaded", 0+@seq, "sequences.");
  return \@seq;
}
#......................................................................................
